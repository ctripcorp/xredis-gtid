# Creates a master-slave pair and breaks the link continuously to force
# partial resyncs attempts, all this while flooding the master with
# write queries.
#
# You can specify backlog size, ttl, delay before reconnection, test duration
# in seconds, and an additional condition to verify at the end.
#
# If reconnect is > 0, the test actually try to break the connection and
# reconnect with the master, otherwise just the initial synchronization is
# checked for consistency.
proc start_write_load_on_db {host port seconds db {key ""} {size 0} {sleep 0}} {
    set tclsh [info nameofexecutable]
    exec $tclsh tests/helpers/gen_write_load.tcl $host $port $seconds $::tls $db $key $size $sleep &
}

proc assert_partial_resync_rejected {sync_full_before sync_partial_err_before sync_partial_ok_before} {
    set sync_partial_err [s -1 sync_partial_err]
    if {$sync_partial_err > $sync_partial_err_before} {
        return
    }
    set sync_full [s -1 sync_full]
    if {$sync_full > $sync_full_before} {
        return
    }
    set sync_partial_ok [s -1 sync_partial_ok]
    fail "Expected rejected partial resync (sync_partial_ok=$sync_partial_ok->$sync_partial_ok_before sync_partial_err=$sync_partial_err->$sync_partial_err_before sync_full=$sync_full->$sync_full_before)"
}

proc test_psync {descr duration backlog_size backlog_ttl delay cond mdl sdl reconnect} {
    start_server {tags {"repl"} overrides {gtid-enabled yes}} {
        start_server {overrides {gtid-enabled yes gtid-xsync-max-gap 0}} {

            set master [srv -1 client]
            set master_host [srv -1 host]
            set master_port [srv -1 port]
            set slave [srv 0 client]

            $master config set repl-backlog-size $backlog_size
            $master config set repl-backlog-ttl $backlog_ttl
            $master config set repl-diskless-sync $mdl
            $master config set repl-diskless-sync-delay 1
            $slave config set repl-diskless-load $sdl

            # In SWAP mode with ASAN the diskless (socket) RDB fork has large
            # shadow-memory overhead, making the RORDB streaming significantly
            # slower.  Unlike disk-based RDB, the master does NOT send keepalive
            # newlines to slaves waiting on a socket-based bgsave.
            #
            # When repl-diskless-load is "disabled", the slave writes the received
            # RDB to a temp file and then loads it via rdbLoad — a blocking
            # operation that prevents the slave from sending ACKs.  If rdbLoad
            # takes longer than repl-timeout, the master drops the slave while
            # it is still loading, causing a repeated full-resync livelock.
            #
            # Use a generous timeout (600 s) to cover both the RORDB stream time
            # and the subsequent rdbLoad under ASAN instrumentation overhead.
            if {$::swap && $::asan} {
                $master config set repl-timeout 600
                $slave config set repl-timeout 600
            }

            if {$::swap} {
                set use_sustained_write_load [expr {$::asan && ($backlog_size == 100 || $backlog_ttl == 1)}]
                if {$use_sustained_write_load} {
                    # Keep write load active for the entire reconnect window
                    # without inflating the dataset. Rewriting a few fixed keys
                    # is enough to advance GTID / backlog and keeps diskless
                    # full-syncs fast under ASAN.
                    set write_cmds_before [status $master total_commands_processed]
                    set load_handle0 [start_write_load_on_db $master_host $master_port 30 0 psync-load-0]
                    set load_handle1 [start_write_load_on_db $master_host $master_port 30 1 psync-load-1]
                    set load_handle2 [start_write_load_on_db $master_host $master_port 30 2 psync-load-2]
                } else {
                    set bg_limit [expr {$::asan ? 5000 : 100000}]
                    set load_handle0 [start_bg_complex_data $master_host $master_port 0 $bg_limit]
                    set load_handle1 [start_bg_complex_data $master_host $master_port 1 $bg_limit]
                }
            } else {
                set load_handle0 [start_bg_complex_data $master_host $master_port 9 100000]
                set load_handle1 [start_bg_complex_data $master_host $master_port 11 100000]
                set load_handle2 [start_bg_complex_data $master_host $master_port 12 100000]
            }

            test {Slave should be able to synchronize with the master} {
                $slave slaveof $master_host $master_port
                wait_for_condition 500 100 {
                    [lindex [r role] 0] eq {slave} &&
                    [lindex [r role] 3] eq {connected}
                } else {
                    fail "Replication not started."
                }
            }

            # Check that the background clients are actually writing.
            test {Detect write load to master} {
                if {$::swap && $use_sustained_write_load} {
                    wait_for_condition 50 1000 {
                        [status $master total_commands_processed] > ($write_cmds_before + 100)
                    } else {
                        fail "Can't detect write load from background clients."
                    }
                } else {
                    wait_for_condition 50 1000 {
                        [$master dbsize] > 100
                    } else {
                        fail "Can't detect write load from background clients."
                    }
                }
            }

            test "Test replication partial resync: $descr (diskless: $mdl, $sdl, reconnect: $reconnect)" {
                set sync_full_before [s -1 sync_full]
                set sync_partial_ok_before [s -1 sync_partial_ok]
                set sync_partial_err_before [s -1 sync_partial_err]
                # Now while the clients are writing data, break the maste-slave
                # link multiple times.
                if ($reconnect) {
                    for {set j 0} {$j < $duration*10} {incr j} {
                        after 100

                        if {($j % 20) == 0} {
                            catch {
                                if {$delay} {
                                    $slave multi
                                    $slave client kill $master_host:$master_port
                                    $slave debug sleep $delay
                                    $slave exec
                                } else {
                                    $slave client kill $master_host:$master_port
                                }
                            }
                        }
                    }
                }

                if {$::swap} {
                    stop_bg_complex_data $load_handle0
                    stop_bg_complex_data $load_handle1
                    if {$use_sustained_write_load} {
                        stop_bg_complex_data $load_handle2
                    }
                } else {
                    stop_bg_complex_data $load_handle0
                    stop_bg_complex_data $load_handle1
                    stop_bg_complex_data $load_handle2
                }

                # Wait for the slave to reach the "online"
                # state from the POV of the master.
                # With bg_limit reduced under ASAN the DB is small (~1k keys),
                # so each full resync completes in <30 s.  600 s (6000 × 100 ms)
                # gives 20× headroom while avoiding the 1000 s waste on genuine
                # failures that inflated the total run time to 2000+ seconds.
                set retry [expr {($::swap && $::asan) ? 6000 : 5000}]
                while {$retry} {
                    set info [$master info]
                    if {[string match {*slave0:*state=online*} $info]} {
                        break
                    } else {
                        incr retry -1
                        after 100
                    }
                }
                if {$retry == 0} {
                    error "assertion:Slave not correctly synchronized"
                }

                # Wait that slave acknowledge it is online so
                # we are sure that DBSIZE and DEBUG DIGEST will not
                # fail because of timing issues. (-LOADING error)
                wait_for_condition 5000 100 {
                    [lindex [$slave role] 3] eq {connected}
                } else {
                    fail "Slave still not connected after some time"
                }

                if {$::swap} {
                    wait_for_condition 1000 50 {
                        [gtid_cmp [get_gtid $slave] [get_gtid $master]]  == 0
                    } else {
                        puts "master: [$master info gtid]"
                        puts "slave: [$slave info gtid]"
                        fail "master slave gtid wait sync err"
                    }
                } else {
                    set retry 10
                    while {$retry && ([$master debug digest] ne [$slave debug digest])}\
                    {
                        after 1000
                        incr retry -1
                    }
                    assert {[$master dbsize] > 0}
                    if {[$master debug digest] ne [$slave debug digest]} {
                        set csv1 [csvdump r]
                        set csv2 [csvdump {r -1}]
                        set fd [open /tmp/repldump1.txt w]
                        puts -nonewline $fd $csv1
                        close $fd
                        set fd [open /tmp/repldump2.txt w]
                        puts -nonewline $fd $csv2
                        close $fd
                        puts "Master - Replica inconsistency"
                        puts "Run diff -u against /tmp/repldump*.txt for more info"
                    }
                    assert_equal [r debug digest] [r -1 debug digest]
                    assert_equal [gtid_cmp [get_gtid $slave] [get_gtid $master]] 0
                }

                eval $cond
            }
        }
    }
}

foreach mdl {no yes} {
    foreach sdl {disabled swapdb} {
        test_psync {no reconnection, just sync} 6 1000000 3600 0 {
        } $mdl $sdl 0

        test_psync {ok psync} 6 100000000 3600 0 {
        assert {[s -1 sync_partial_ok] > 0}
        } $mdl $sdl 1

        test_psync {no backlog} 6 100 3600 0.5 {
        assert_partial_resync_rejected $sync_full_before $sync_partial_err_before $sync_partial_ok_before
        } $mdl $sdl 1

        test_psync {ok after delay} 3 100000000 3600 3 {
        assert {[s -1 sync_partial_ok] > 0}
        } $mdl $sdl 1

        test_psync {backlog expired} 3 100000000 1 3 {
        assert_partial_resync_rejected $sync_full_before $sync_partial_err_before $sync_partial_ok_before
        } $mdl $sdl 1
    }
}
